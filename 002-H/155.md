Gorgeous Cider Barracuda

medium

# Migration done on unaccessable users will result in a loss of their rewards

## Summary
The Truffle contracts support a migration functionality for migrating user's vesting and staking info from one account to another. This functionality does not account for the generated rewards of the given user at the moment of migration.

## Vulnerability Detail
[*TrufVesting.migrateUser()*](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L302-L338) is used when a user has lost their private key. The function allows the owner of the contract to move the current vesting data for the old user to a new user. It then calls [*VotingEscrowTruf.migrateVestingLock()*](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol#L217-L252) to migrate the staking data from the old user to the new one, which is basically unstaking on the account of the old user and staking the same amount on the account of the new user.

The problem is that neither `TrufVesting.migrateUser()` nor `VotingEscrowTruf.migrateVestingLock()` transfer the old user's rewards to the new user. The only way to retrieve the rewards is by calling [*VirtualStakingRewards.getReward()*](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/staking/VirtualStakingRewards.sol#L126-L133) 

```solidity
    function getReward(address user) public updateReward(user) returns (uint256 reward) {
        reward = rewards[user];
        if (reward != 0) {
            rewards[user] = 0;
            IERC20(rewardsToken).safeTransfer(user, reward);
            emit RewardPaid(user, reward);
        }
    }
```
For this to happen, the old user's address has to be passed to `getReward`. The tokens will then be send to this address. This is undesirable because the migration feature will be used when a user has lost their private key, i.e their account is not available, and the funds will be locked forever.

```solidity
/**
     * @notice Migrate owner of vesting. Used when user lost his private key
     * @dev Only admin can migrate users vesting
     * @param categoryId Category id
     * @param vestingId Vesting id
     * @param prevUser previous user address
     * @param newUser new user address
 */
```

## Impact
The already accumulated rewards for an unaccessible account will be forever lost, even after migrating.

## Code Snippet
`TrufVesting.migrateUser():`

```solidity
    /**
     * @notice Migrate owner of vesting. Used when user lost his private key
     * @dev Only admin can migrate users vesting
     * @param categoryId Category id
     * @param vestingId Vesting id
     * @param prevUser previous user address
     * @param newUser new user address
     */
    function migrateUser(uint256 categoryId, uint256 vestingId, address prevUser, address newUser) external onlyOwner {
        UserVesting storage prevVesting = userVestings[categoryId][vestingId][prevUser];
        UserVesting storage newVesting = userVestings[categoryId][vestingId][newUser];

        if (newVesting.amount != 0) {
            revert UserVestingAlreadySet(categoryId, vestingId, newUser);
        }
        if (prevVesting.amount == 0) {
            revert UserVestingDoesNotExists(categoryId, vestingId, prevUser);
        }

        newVesting.amount = prevVesting.amount;
        newVesting.claimed = prevVesting.claimed;
        newVesting.startTime = prevVesting.startTime;

        uint256 lockupId = lockupIds[categoryId][vestingId][prevUser];
        uint256 newLockupId;

        if (lockupId != 0) {
            newLockupId = veTRUF.migrateVestingLock(prevUser, newUser, lockupId - 1) + 1;
            lockupIds[categoryId][vestingId][newUser] = newLockupId;
            delete lockupIds[categoryId][vestingId][prevUser];

            newVesting.locked = prevVesting.locked;
        }
        delete userVestings[categoryId][vestingId][prevUser];

        emit MigrateUser(categoryId, vestingId, prevUser, newUser, newLockupId);
    }
```

`VotingEscrowTruf.migrateVestingLock():`
```solidity
    /**
     * @notice Migrate lock to another user
     * @param oldUser Old user address
     * @param newUser New user address
     * @param lockupId the id of the old user's lockup to migrate
     * @return newLockupId the id of new user's migrated lockup
     */
    function migrateVestingLock(address oldUser, address newUser, uint256 lockupId)
        external
        onlyVesting
        returns (uint256 newLockupId)
    {
        if (oldUser == newUser) {
            revert NotMigrate();
        }
        if (newUser == address(0)) {
            revert ZeroAddress();
        }
        Lockup memory oldLockup = lockups[oldUser][lockupId];
        if (!oldLockup.isVesting) {
            revert NoAccess();
        }

        uint256 points = oldLockup.points;
        stakingRewards.withdraw(oldUser, points);
        _burn(oldUser, points);

        newLockupId = lockups[newUser].length;
        lockups[newUser].push(oldLockup);
        _mint(newUser, points);
        stakingRewards.stake(newUser, points);

        delete lockups[oldUser][lockupId];

        emit Migrated(oldUser, newUser, lockupId, newLockupId);
    }
```
## Tool used

Manual Review

## Recommendation
Add a function in `StakingRewards.sol` and `VirtualStakingRewards.sol` that transfers the rewards of one user to another and is callable only by the protocol. Use that function whenever a migration occurs.
