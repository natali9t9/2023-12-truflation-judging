Suave Hazel Mouse

medium

# Rewards remain unclaimable in vesting migration due to private key loss

## Summary
This report addresses the issue identified in `TrufVesting.migrateUser()`, which fails to handle the transfer of unclaimable rewards during the migration of vesting details. This could lead to rewards being inaccessible in cases where a user loses access to their private key and requires migration to a new account.

## Vulnerability Detail
[`migrateUser()`](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L329) is designed to transfer vesting details from one user to another. However, it does not include the transfer of unclaimed rewards that may have accumulated in the original user's account. As a result, these rewards remain tied to the original account, which may become inaccessible if the private key is lost.

As can be seen from `VotingEscrowTruf.migrateVestingLock()` below, `stakingRewards.withdraw(oldUser, points)` only caters from unstaking the old user in moving the `Lockup` but not the rewards:

```solidity
    function migrateVestingLock(address oldUser, address newUser, uint256 lockupId)
        external
        onlyVesting
        returns (uint256 newLockupId)
    {
        if (oldUser == newUser) {
            revert NotMigrate();
        }
        if (newUser == address(0)) {
            revert ZeroAddress();
        }
        Lockup memory oldLockup = lockups[oldUser][lockupId];
        if (!oldLockup.isVesting) {
            revert NoAccess();
        }

        uint256 points = oldLockup.points;
        stakingRewards.withdraw(oldUser, points);
        _burn(oldUser, points);

        newLockupId = lockups[newUser].length;
        lockups[newUser].push(oldLockup);
        _mint(newUser, points);
        stakingRewards.stake(newUser, points);

        delete lockups[oldUser][lockupId];

        emit Migrated(oldUser, newUser, lockupId, newLockupId);
    }
```
Evidently, `VirtualStakingRewards.getReward()` will not benefit the situation as the rewards is still tied to the old user:

```solidity
    function getReward(address user) public updateReward(user) returns (uint256 reward) {
        reward = rewards[user];
        if (reward != 0) {
            rewards[user] = 0;
            IERC20(rewardsToken).safeTransfer(user, reward);
            emit RewardPaid(user, reward);
        }
    }
```
## Impact
Users who lose access to their private keys and undergo a vesting migration will be unable to access rewards accumulated in their previous account. This oversight could lead to user dissatisfaction and potentially impact the trust in the system's ability to safeguard user interests.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L329
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol#L241
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/staking/VirtualStakingRewards.sol#L126-L133

## Tool used

Manual Review

## Recommendation
Implement a mechanism within the `migrateVestingLock` function to also transfer unclaimed rewards from the old user to the new user. This may be done via a separate function in VirtualStakingRewards.sol for reward migration and automate/incorporate reward claims during the migration process.
