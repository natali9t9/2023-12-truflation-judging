Savory Peanut Falcon

medium

# An expired lock can be extended with a past `end` timestamp while increasing points

## Summary

The function description in `_extendLock()` in `VotingEscrowTruf.sol` states: 

> The stake end time is computed from the current time + duration, just like it is for new stakes. So a new stake for seven days duration and an old stake extended with a seven days duration would have the same end.

But the function calculates the new end time by adding the new specified duration to the previous end time instead of the current time.

## Vulnerability Detail

This allows for an user to extend a past lock and have the new expiry date to be in the past as well. In addition to this, the function description does not match the implementation. 

The new end is calculated at [#L351](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol#L351) in `VotingEscrowTruf.sol`:

```solidity
    function _extendLock(address user, uint256 lockupId, uint256 duration, bool isVesting) internal {
        // duration checked inside previewPoints
        Lockup memory lockup = lockups[user][lockupId];

        //...SNIP...

        uint256 oldEnd = lockup.end;
        uint256 oldPoints = lockup.points;
        uint256 newDuration = lockup.duration + duration;

        (uint256 newPoints,) = previewPoints(amount, newDuration); // <-- EXTRA POINTS ARE CALCULATED REGARDLESS

        //...SNIP...
        
        uint256 newEnd = oldEnd + duration; // <-- NEW END IS OLD END PLUS DURATION

        uint256 mintAmount = newPoints - oldPoints;

        lockup.end = uint128(newEnd);
        lockup.duration = uint128(newDuration);
        lockup.points = newPoints;

        lockups[user][lockupId] = lockup;

        stakingRewards.stake(user, mintAmount);
        _mint(user, mintAmount);

       //...SNIP...
    }
```

## Impact

Alice could abuse this by staking and locking a large amount of tokens for the minimum duration accepted. Then when necessary she can extend the lock with an end time that it's in the past by a few seconds relative to the timestamp when the transaction was executed, keeping the funds unlocked but increasing her `points`.

Alice increases her `points` and mints `VotingEscrowTruf ERC20Votes tokens` herself risk-free because she can unstake whenever she wants. Innocent users have to effectively lock their tokens to get the same result, but they have to take the risk of actually locking their tokens to do so.

It's hard to find information about the token of `VotingEscrowTruf.sol` but it's `ERC20Votes` token, meaning it might be used for governance votes at some point.

## Code Snippet

https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol#L351

## POC 

Copy and paste this test function in `VotingEscrowTruf.t.sol` to test the issue:

```solidity
    function testExtendLockPast() external {
        console.log("POC: Extend duration of lockup in the past");

        uint256 amount = 100e18;
        uint256 duration = 30 days;

        console.log("1. Alice stakes 100e18 tokens for 30 days");
        _stake(amount, duration, alice, alice);

        console.log("2. 100 days pass by");
        vm.warp(block.timestamp + 100 days);


        uint256 extendDuration = 60 days;
        vm.startPrank(alice);

        (,,,uint256 beforePoints,) = veTRUF.lockups(alice, 0);

        console.log("3. Alice extends her past lock by 60 days. This results in the lock being expired 10 days ago (100 days passed - (30 days initial + 60 days extension))");
        veTRUF.extendLock(0, extendDuration);

        (,,  uint128 _ends, uint256 afterPoints,) = veTRUF.lockups(alice, 0);
        console.log("   > Alice has %s more points", afterPoints - beforePoints);
        assertGt(block.timestamp, _ends);

        console.log("4. Alice can unstake her tokens because the lock is expired.");
        veTRUF.unstake(0);
        vm.stopPrank();
    }
```
## Tool used

Manual Review

## Recommendation

Don't allow the extension of an expired lock. If the extension of an expired lock needs to be allowed make sure to not account for the time when the lock expired to the current timestamp when calculating the amount of `points` the user should receive. 
