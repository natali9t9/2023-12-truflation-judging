Unique Lace Ape

medium

# Adversary can moderately abuse extension of expired locks

## Summary
Sophisticated users can abuse the extension mechanism to be able to vote for proposals while reserving the right to withdraw their tokens at any time; or to get more voting power in comparison to users who create new locks with the same `amount` and `duration`.
## Vulnerability Detail
### Proof of Concept
**t = 0**
Alice locks 100e18 TRUF for 1 month (let's assume `minStakeDuration = 1 month`), to be able to vote in the upcoming proposals.

**t = 1 month**
Alice's lock expires. She is now able to unstake, but does not do it just yet, as she does not see a good use for her TRUF and is aware of the bug, so her TRUF just stays in veTruf contract. Also no proposals that Alice is interested in are going on, so she does not need to extend her lock.

**t = 12 months**
There's finally a proposal that Alice is interested in. She calls `extendLock(duration = 12 months)`, getting 13x of her original voting power for the next month. But the updated lockup will actually expire in just **1 month, instead of 1 year**:
```solidity
uint256 oldEnd = lockup.end;
/*...*/
uint256 newEnd = oldEnd + duration;
/*...*/
lockup.end = uint128(newEnd);
/*...*/
lockups[user][lockupId] = lockup;
```
So Alice was able to extend her expired lock and get the same voting power as honest users who locked 100e18 TRUF for 13 months, **while being able to withdraw her TRUF at any moment during `1 month < t < 12 months` and `t > 13 months`**, or 2 months of total lock time instead of 13.

Similarly, at `t = 12 months`, if Alice is comfortable actually locking TRUF for the max duration, she could call `extendLock(duration = 35 months)` and be able to unstake her TRUF in 2 years instead of 3.
 
### Foundry PoC
Add to VotingEscrowTruf.t.sol
```solidity
    function testExtendLock_PoC() external {
        console.log("Extend duration of lockup");

        uint256 amount = 100e18;
        uint256 duration = 365 days / 12; // 1 month

        // alice stakes for 1 month
        _stake(amount, duration, alice, alice);

        vm.prank(alice);
        (,,, uint256 points,) = veTRUF.lockups(alice, 0);

        skip(365 days);

        uint256 extendDuration = 365 days;
        vm.prank(alice);

        // alice extends for 12 months
        veTRUF.extendLock(0, extendDuration);
        (,, uint256 endTime, uint256 newPoints,) = veTRUF.lockups(alice, 0);

        // Alice will be able to withdraw her TRUF in 1 month (instead of 1 year)
        assertEq(endTime, block.timestamp + 365 days / 12);

        // Alice's voting power went up 13x
        assertApproxEqAbs(newPoints, 13 * points, 10); // 10 wei diff due to precision loss
    }
```
## Impact

Users who didn't lock their tokens can have the same voting power as those who did.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol#L333-L366
## Tool used

Manual Review

## Recommendation
```diff
    function _extendLock(address user, uint256 lockupId, uint256 duration, bool isVesting) internal {
        // duration checked inside previewPoints
        Lockup memory lockup = lockups[user][lockupId];
        if (lockup.isVesting != isVesting) {
            revert NoAccess();
        }
+       require(lockup.end <= block.timestamp, "lock expired");

        uint256 amount = lockup.amount;
        uint256 oldEnd = lockup.end;
        uint256 oldPoints = lockup.points;
        uint256 newDuration = lockup.duration + duration;


        (uint256 newPoints,) = previewPoints(amount, newDuration);


        if (newPoints <= oldPoints) {
            revert NotIncrease();
        }


        uint256 newEnd = oldEnd + duration;


        uint256 mintAmount = newPoints - oldPoints;


        lockup.end = uint128(newEnd);
        lockup.duration = uint128(newDuration);
        lockup.points = newPoints;


        lockups[user][lockupId] = lockup;


        stakingRewards.stake(user, mintAmount);
        _mint(user, mintAmount);


        emit Unstake(user, isVesting, lockupId, amount, oldEnd, oldPoints);
        emit Stake(user, isVesting, lockupId, amount, newEnd, newPoints);
    }
```