Abundant Infrared Yak

medium

# VirtualStakingRewards.sol#notifyRewardAmount function could not ensure that the provided reward amount is not more than the balance.

## Summary
`VirtualStakingRewards.sol#notifyRewardAmount` function check the balance of contract to prevent the provided rewards from being depleted by highly estimated `rewardRate` than the actual value.
However, the balance may include the amount that has not yet been claimed by users.
Thus, `rewardRate` will be inflated, so that the rewards is overpaid to the users and eventually depletion may occur.

## Vulnerability Detail
`truflation-contracts\test\staking\VirtualStakingRewards.t.sol#testNotifyRewardAmount_Revert_WhenRewardIsLowerThanBalance` function is following.
```solidity
    function testNotifyRewardAmount_Revert_WhenRewardIsLowerThanBalance() external {
        console.log("Should revert if notified reward amount is lower than balance");

        uint256 rewards = 100e18;
        vm.startPrank(rewardsDistribuion);

        trufToken.transfer(address(trufStakingRewards), rewards);

        vm.expectRevert(abi.encodeWithSignature("InsufficientRewards()"));
        trufStakingRewards.notifyRewardAmount(101e18);

        vm.stopPrank();
    }
``` 
This means that `VirtualStakingRewards.sol#notifyRewardAmount` function must revert when it is called with a parameter (`101e18`) that is larger than the amount of tokens that the administrator has transferred (`100e18`).

`notifyRewardAmount` function is following.
```solidity
    function notifyRewardAmount(uint256 reward) external onlyRewardsDistribution updateReward(address(0)) {
        if (block.timestamp >= periodFinish) {
            rewardRate = reward / rewardsDuration;
        } else {
            uint256 remaining = periodFinish - block.timestamp;
            uint256 leftover = remaining * rewardRate;
            rewardRate = (reward + leftover) / rewardsDuration;
        }

153:    // Ensure the provided reward amount is not more than the balance in the contract.
        // This keeps the reward rate in the right range, preventing overflows due to
        // very high values of rewardRate in the earned and rewardsPerToken functions;
        // Reward + leftover must be less than 2^256 / 10^18 to avoid overflow.
        uint256 balance = IERC20(rewardsToken).balanceOf(address(this));
158:    if (rewardRate > balance / rewardsDuration) {
            revert InsufficientRewards();
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(reward);
    }
```
`L158` uses the value of balance of contract to prevent reward from being depleted.

However, the balance may contain a large amount of tokens that have not yet been claimed by users.
That is, certain amount of tokens has been computed for rewards of users, but users have not called the `getReward` function yet, so they can still be included in the balance of the contract, not in the balance of users.

Hence, the following example is possible.
1. Administrator deploys `VirtualStakingReward` contract with `rewardsDuration=7 days`.
2. Administrator transfers tokens of `amount=7*86400*1e18` to `VirtualStakingReward` and calls `notifyRewardAmount(amount)`. Then the `rewardRate` will be `rewardRate=1e18`.
3. Users uses `VirtualStakingReward` contract through `stake` and `withdraw` functions, but no user claim one's reward from it.
4. After 7 days later, administrator transfer again tokens of `amount=7*86400*1e18` to `VirtualStakingReward` and calls `notifyRewardAmount(2*amount)` mistakenly similar to the above test code. Thus it will be `rewardRate=2e18`.
5. Since no user claim one's reward, the balance of `VirtualStakingReward` will be `2*7*86400*1e18 = 2*amount`, therefore the condition of `L158` holds and the function does not revert.
6. Since `rewardRate` was calculated twofold large, the reward will be depleted after 3.5 days and after then users' claim request may revert.

## Impact
Since `rewardRate` of `VirtualStakingReward` is miscalculated, users can be provided with more than necessary tokens as reward.
After all, reward will be depleted and thereafter claim requests may be reverted.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/staking/VirtualStakingRewards.sol#L158

## Tool used
Manual Review

## Recommendation
`VirtualStakingRewards.sol#notifyRewardAmount` function must subtract the amount of reward that has to be already paid to users from balance.

For example we can introduce new state variables `totoalToPay` and `totalPaid`.
Then the `totoalToPay` should be updated in `updateReward` moidfier and `totalPaid` should be updated in `getReward` function respectively.
Finally we insert `balance -= (totalToPay - totalPaid)` between `L157` and `L158` of `VirtualStakingRewards.sol`.
