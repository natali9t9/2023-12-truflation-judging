Striped Chocolate Kitten

medium

# Users will lose all their rewards in the `VirtualStakingRewards` after migration

## Summary

When migrating the owner of vesting, the `VotingEscrowTruf.migrateVestingLock()` function only transfers the staked `TRUF` to the new user, but it ignores the rewards in the old user, leading to a loss of funds for the old user.

## Vulnerability Detail

According to the comments in the function `TrufVesting.migrateUser()`, when a user loses their private key, the owner could migrate the ownership of vesting to a new user. If there are locked `veTRUF`, `TrufVesting.migrateUser()` will invoke `veTRUF.migrateVestingLock` to transfer them.

```solidity
    /**
     * @notice Migrate owner of vesting. Used when user lost his private key
     * @dev Only admin can migrate users vesting
     * @param categoryId Category id
     * @param vestingId Vesting id
     * @param prevUser previous user address
     * @param newUser new user address
     */
    function migrateUser(uint256 categoryId, uint256 vestingId, address prevUser, address newUser) external onlyOwner {
        UserVesting storage prevVesting = userVestings[categoryId][vestingId][prevUser];
        UserVesting storage newVesting = userVestings[categoryId][vestingId][newUser];

        if (newVesting.amount != 0) {
            revert UserVestingAlreadySet(categoryId, vestingId, newUser);
        }
        if (prevVesting.amount == 0) {
            revert UserVestingDoesNotExists(categoryId, vestingId, prevUser);
        }

        newVesting.amount = prevVesting.amount;
        newVesting.claimed = prevVesting.claimed;
        newVesting.startTime = prevVesting.startTime;

        uint256 lockupId = lockupIds[categoryId][vestingId][prevUser];
        uint256 newLockupId;

        if (lockupId != 0) {
            newLockupId = veTRUF.migrateVestingLock(prevUser, newUser, lockupId - 1) + 1;
            lockupIds[categoryId][vestingId][newUser] = newLockupId;
            delete lockupIds[categoryId][vestingId][prevUser];

            newVesting.locked = prevVesting.locked;
        }
        delete userVestings[categoryId][vestingId][prevUser];

        emit MigrateUser(categoryId, vestingId, prevUser, newUser, newLockupId);
    }
```

The function `migrateVestingLock` will first withdraw the staked `TRUF` from old user and then stake the `TRUF` to the new user.

```solidity
    function migrateVestingLock(address oldUser, address newUser, uint256 lockupId)
        external
        onlyVesting
        returns (uint256 newLockupId)
    {
        if (oldUser == newUser) {
            revert NotMigrate();
        }
        if (newUser == address(0)) {
            revert ZeroAddress();
        }
        Lockup memory oldLockup = lockups[oldUser][lockupId];
        if (!oldLockup.isVesting) {
            revert NoAccess();
        }
//@audit withdrwa the old points
        uint256 points = oldLockup.points;
        stakingRewards.withdraw(oldUser, points);
        _burn(oldUser, points);
//@audit stake to new user, no rewards is transferred
        newLockupId = lockups[newUser].length;
        lockups[newUser].push(oldLockup);
        _mint(newUser, points);
        stakingRewards.stake(newUser, points);

        delete lockups[oldUser][lockupId];

        emit Migrated(oldUser, newUser, lockupId, newLockupId);
    }
```

Howeve, the `stake` and `withdraw` functions only change the `_totalSupply` and the `_balances`, and the rewards in the old user are not transferred to the new user. These rewards can never be recovered, because the old user has "*lost their private key*".

```solidity
    function stake(address user, uint256 amount) external updateReward(user) onlyOperator {
        if (amount == 0) {
            revert ZeroAmount();
        }
        if (user == address(0)) {
            revert ZeroAddress();
        }
        _totalSupply += amount;
        _balances[user] += amount;
        emit Staked(user, amount);
    }
```

```solidity
    function withdraw(address user, uint256 amount) public updateReward(user) onlyOperator {
        if (amount == 0) {
            revert ZeroAmount();
        }
        _totalSupply -= amount;
        _balances[user] -= amount;
        emit Withdrawn(user, amount);
    }
```

## Impact

Users will lose all their rewards in the `VirtualStakingRewards` contract after migration.

## Code Snippet

https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L302-L338
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L224-L252
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/staking/VirtualStakingRewards.sol#L105-L115
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/staking/VirtualStakingRewards.sol#L117-L124

## Tool used

Manual Review

## Recommendation
It's recommended to transfer the rewards from the old user to the new user when migrating.