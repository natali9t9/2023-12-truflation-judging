Quiet Ceramic Oyster

medium

# Governance: `VotingEscrowTruf.sol#migrateVestingLock` results in orphaned voting power.

## Summary

When an [`owner`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/fd81a96f01cc42ef1c9a5399364968d0e07e9e90/contracts/access/Ownable.sol#L21) makes a call to [`migrateVestingLock(address,address,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L224C32-L224C84), an incomplete migration is enacted, resulting in a newly migrated token lockup position which lacks possession of the original voting power.

## Vulnerability Detail

When [`migrateVestingLock(address,address,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L224C32-L224C84) is called, a new [`Lockup`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/interfaces/IVotingEscrow.sol#L6) is initialized by [migrating the parameters](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L240C9-L249C43) of an [`oldLockup`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L235C23-L235C32):

```solidity
uint256 points = oldLockup.points;
stakingRewards.withdraw(oldUser, points);
_burn(oldUser, points);

newLockupId = lockups[newUser].length;
lockups[newUser].push(oldLockup);
_mint(newUser, points);
stakingRewards.stake(newUser, points);

delete lockups[oldUser][lockupId];
```

However, this does not take into account the absence of delegated voting power, which can be initialized when first [creating a staked position](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L169C1-L172C10):

```solidity
if (delegates(to) == address(0)) {
    // Delegate voting power to the receiver, if unregistered
    _delegate(to, to);
}
```

In the sequence below, we demonstrate that after a call to [`migrateVestingLock(address,address,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L224C32-L224C84), the voting power is lost not just to the delegated position, but all of the associated addresses entirely:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import "forge-std/console.sol";
import "forge-std/Test.sol";
import "../../src/token/TruflationToken.sol";
import "../../src/token/TrufVesting.sol";
import "../../src/token/VotingEscrowTruf.sol";
import "../../src/staking/VirtualStakingRewards.sol";

import "./VotingEscrowTruf.t.sol";

contract VotingEscrowTrufSherlockTest is VotingEscrowTrufTest {

    function test_sherlock_StakeVesting() external {

        uint256 amount = 100e18;
        uint256 duration = 30 days;

        /* bob_delegates_votes_to_alice */
        vm.prank(bob);
        veTRUF.delegate(alice);

        vm.startPrank(vesting);

        /* no_voting_power */
        assertEq(veTRUF.getVotes(bob), 0);
        assertEq(veTRUF.getVotes(alice), 0);
        assertEq(veTRUF.getVotes(carol), 0);

        /* bob_vests */
        assertEq(veTRUF.stakeVesting(amount, duration, bob), 0, "Lockup id is invalid");

        /* resulting_voting_power */
        assertEq(veTRUF.getVotes(bob), 0);
        assertEq(veTRUF.getVotes(alice), 2739726027397260273) /* delegated votes */;
        assertEq(veTRUF.getVotes(carol), 0);

        /* bob_migrate_vests_to_carol */
        veTRUF.migrateVestingLock(bob, carol, 0);
        
        /* no_voting_power (incorrect) */
@>      assertEq(veTRUF.getVotes(bob), 0);
@>      assertEq(veTRUF.getVotes(alice), 0);
@>      assertEq(veTRUF.getVotes(carol), 0);

        vm.stopPrank();
    }
    
}
```

In conclusion, the `2739726027397260273` votes originally belonging to `bob` that were subsequently delegated to `alice` are lost in entirety after a call to [`migrateVestingLock(address,address,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/VotingEscrowTruf.sol#L224C32-L224C84) when migrating `bob`'s position to `carol`.

## Impact

The loss of voting power for a vested position.

## Code Snippet

```solidity
/**
 * @notice Migrate lock to another user
 * @param oldUser Old user address
 * @param newUser New user address
 * @param lockupId the id of the old user's lockup to migrate
 * @return newLockupId the id of new user's migrated lockup
 */
function migrateVestingLock(address oldUser, address newUser, uint256 lockupId)
    external
    onlyVesting
    returns (uint256 newLockupId)
{
    if (oldUser == newUser) {
        revert NotMigrate();
    }
    if (newUser == address(0)) {
        revert ZeroAddress();
    }
    Lockup memory oldLockup = lockups[oldUser][lockupId];
    if (!oldLockup.isVesting) {
        revert NoAccess();
    }

    uint256 points = oldLockup.points;
    stakingRewards.withdraw(oldUser, points);
    _burn(oldUser, points);

    newLockupId = lockups[newUser].length;
    lockups[newUser].push(oldLockup);
    _mint(newUser, points);
    stakingRewards.stake(newUser, points);

    delete lockups[oldUser][lockupId];

    emit Migrated(oldUser, newUser, lockupId, newLockupId);
}
```

## Tool used

Manual Review, Visual Studio Code, Foundry

## Recommendation

### [VotingEscrowTruf.sol](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/VotingEscrowTruf.sol)

When migrating an old position, we must ensure the delegated votes are also handled as part of the migration:

```diff
        newLockupId = lockups[newUser].length;
        lockups[newUser].push(oldLockup);
        _mint(newUser, points);
        stakingRewards.stake(newUser, points);
+
+       if (delegates(to) == address(0)) {
+           // Delegate voting power to the receiver, if unregistered
+           _delegate(to, to);
+       }

        delete lockups[oldUser][lockupId];
```
