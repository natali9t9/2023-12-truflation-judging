Unique Lace Ape

medium

# Users with multiple merkle leafs will not be able to claim all TRUF from TrufMigrator

## Summary

## Vulnerability Detail

As we can see from the implementation of the `migrate` function, in particular the usage of `index` variable and tracking of migratedAmount: 

```solidity
migratedAmount[msg.sender] = amount;
uint256 migrateAmount = amount - _migratedAmount;
```

the function is expected to handle multiple merkle leafs for the same account. Once there's such merkle tree, it will be handled incorrectly, leading to loss of TRUF for the users with multiple leafs.

### Proof of Concept
Alice has 2 entries, for 100 and 200 TRUF respectively. 

If she chooses to claim 100 and then 200, the following will occur:
1. Alice successfully claims 100.
2. Alice tries to claim 200:
```solidity
        uint256 _migratedAmount = migratedAmount[msg.sender]; // == 100

        if (amount <= _migratedAmount) { // (200 <= 100) == false
            revert AlreadyMigrated();
        }
        migratedAmount[msg.sender] = amount; // == 200

        uint256 migrateAmount = amount - _migratedAmount; // == 100
        trufToken.safeTransfer(msg.sender, migrateAmount); 
```
So she is transferred 100 again, instead of 200. In total, she receives 100+100 instead of 100+200.

If Alice chooses to claim 200 and then 100:
1. Alice successfully claims 200.
2. Alice tries to claim 100:
```solidity
        uint256 _migratedAmount = migratedAmount[msg.sender]; // == 200

        if (amount <= _migratedAmount) { // (100 <= 200) == true
            revert AlreadyMigrated(); // txn reverts
        }
```
So Alice receives just the 200 from the first claim. 

In both scenarios, Alice receives at most 200 TRUF instead of 300.

## Impact
Users with multiple merkle tree entries are unable to claim some of their TRUF.
## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufMigrator.sol#L48-L67
## Tool used

Manual Review

## Recommendation
Track each index separately:

```diff
-   mapping(address => uint256) public migratedAmount;
+   mapping(uint256 => bool) public claimedIndexes;  
/*...*/
    function migrate(uint256 index, uint256 amount, bytes32[] calldata proof) external {  // @audit-info just withdras tokens, no migration
        bytes32 leaf = keccak256(abi.encode(msg.sender, index, amount));

        if (MerkleProof.verify(proof, merkleRoot, leaf) == false) {
            revert InvalidProof();
        }
-       uint256 _migratedAmount = migratedAmount[msg.sender];

-       if (amount <= _migratedAmount) {
+       if (claimedIndexes[index] == true) {
            revert AlreadyMigrated();
        }
+       claimedIndexes[index] = true;
-       migratedAmount[msg.sender] = amount;

-       uint256 migrateAmount = amount - _migratedAmount;
-       trufToken.safeTransfer(msg.sender, migrateAmount);
+       trufToken.safeTransfer(msg.sender, amount);
-       emit Migrated(msg.sender, migrateAmount);
+       emit Migrated(msg.sender, amount);
    }
```