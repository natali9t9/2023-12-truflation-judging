Rare Satin Owl

high

# Total vesting time is `initialReleasePeriod + cliff + period`, however admin cannot cancel vesting after just `period` has passed

## Summary

The timeline for a vesting is as follow:

```txt
A ------------- B ---------------- C ------------- D

- A to B: VestingInfo.initialReleasePeriod
- B to C: VestingInfo.cliff
- C to D: VestingInfo.period 
```

The admin has the permission to cancel a user's vesting for any reason, as long as the vesting has not ended. However, we show that after just `VestingInfo.period` has passed, an admin cannot cancel the vesting, even though the vesting is not completed.

## Vulnerability Detail

In the `cancelVesting()` function, there is the following condition check:

```solidity
if (userVesting.startTime + vestingInfos[categoryId][vestingId].period <= block.timestamp) {
    revert AlreadyVested(categoryId, vestingId, user);
}
```

This is not the complete timeline of the vesting, nor is it consistent with any major events in the vesting.

Suppose there is a vesting for a user with:
- 30 days initial release period
- 10 days cliff
- 50 days vesting period

Then said condition prohibits an admin from cancelling the vesting after 50 days, even though it is in the middle of the vesting.

Therefore there is an inconsistency in the vesting timeline formula of `claimable()` and `cancelVesting()`. 

We also provide a coded PoC. Copy paste the test into `TrufVesting.t.sol`:

```solidity
function testCancelVesting_WrongRevertPoC() public {
    console.log("PoC: admin cannot cancel vesting if period passed");
    address dave = address(uint160(uint256(keccak256(abi.encodePacked("Dave")))));

    /* 
        Set up a new vesting for Dave:
        - initialReleasePct is 500 (5%)
        - initialReleasePeriod is 30 days
        - cliff is 10 days
        - period is 50 days
        - total vesting amount is (5e18 + 95e18) tokens
        - period is 1 second, therefore linear real-time vesting
        - starting time is vesting.tgeTime()

        Total vesting time is (30+10+50) = 90 days

        This PoC shows admin cannot cancel after day 50
    */
    console.log("Setup Dave 90-day vesting");
    vm.startPrank(owner);
    trufToken.approve(address(vesting), type(uint256).max);

    vesting.setVestingCategory(type(uint256).max, "PoC category", 100e18, false); // 100 tokens
    vesting.setVestingInfo(0, type(uint256).max, TrufVesting.VestingInfo(
        500, 30 days, 10 days, 50 days, 1
    ));
    vesting.setUserVesting(0, 0, dave, 0, 100e18);
    vm.stopPrank();
    
    // setting up finished
    // dave should be able to claim after 31 days
    console.log("Day 31: Dave has some claimable tokens");
    (, , ,uint64 daveStartTime) = vesting.userVestings(0, 0, dave);
    vm.warp(daveStartTime + 31 days);
    uint256 claimable31Days = vesting.claimable(0, 0, dave);
    assertNotEq(claimable31Days, 0, "Dave: Claimable amount after 31 days should be non-zero");

    // warp to 51 days
    // dave should still be claimable
    console.log("Day 51: Dave has some claimable tokens");
    vm.warp(daveStartTime + 51 days);
    uint256 claimable51Days = vesting.claimable(0, 0, dave);
    assertNotEq(claimable51Days, 0, "Dave: Claimable amount after 51 days should be non-zero");

    // admin unsuccessfully cancels
    console.log("Day 51: Admin tries to cancel, but not successful");
    vm.startPrank(owner);
    vm.expectRevert(
        abi.encodeWithSignature("AlreadyVested(uint256,uint256,address)", 0, 0, dave)
    );
    vesting.cancelVesting(0, 0, dave, false);
    vm.stopPrank();

    // however, dave is still vesting
    // at day 52, dave's claim amount is greater than day 51
    console.log("Day 52: Dave vesting continues");
    vm.warp(daveStartTime + 52 days);
    uint256 claimable52Days = vesting.claimable(0, 0, dave);
    assertGt(claimable52Days, claimable51Days, "Dave: Claimable amount after 52 days should be greater than 51");

    // PoC complete. Admin cannot cancel vesting after only "period", although it is still vesting
    console.log("PoC complete");
}
```

Then run the test with the following command:
```txt
forge test --match-test testCancelVesting_WrongRevertPoC -vv
```

## Impact

Admin is not able to cancel a vesting even if vesting is not finished.

## Code Snippet

https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L358-L360

## Tool used

Manual Review, Foundry

## Recommendation

The correct conditional check should be

```solidity
if (userVesting.startTime + vestingInfos[categoryId][vestingId].period + vestingInfos[categoryId][vestingId].cliff + vestingInfos[categoryId][vestingId].initialReleasePeriod <= block.timestamp) {}
```

If the condition check was indeed correct and intended, then the formula in `claimable()` is incorrect, and definitions need to be reviewed.
