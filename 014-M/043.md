Quiet Ceramic Oyster

medium

# Loss of Yield: `TrufVesting#setVestingInfo` remains callable after the token generation event has occurred.

## Summary

In [`TrufVesting.sol`](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol), it is possible for an [`admin`]() to change the terms of an active [`VestingInfo`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L94) via a call to [`setVestingInfo(uint256,uint256,VestingInfo)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L463C14-L464C39).

However, this is a stepwise change.

Whilst this operation adjusts the vesting terms actively vested positions will now accrue to, it also influences historical earnings, which has the unintended effect of either rewarding or slashing impacted stakers.

## Vulnerability Detail

It is possible to call [`setVestingInfo(uint256,uint256,VestingInfo)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L463C14-L464C39) to modify the [`VestingInfo`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L94) for a given category even whilst there exist actively vesting positions based upon it. Accordingly, the corresponding value currently staked by the position is changed, as if these had been the terms of staking for the lifetime of the position.

In the scenario below, we demonstrate that staker `alice` suffers a loss of yield due to changes in the vesting terms mid reward cycle:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import "forge-std/console.sol";
import "forge-std/Test.sol";
import "../../src/token/TruflationToken.sol";
import "../../src/token/TrufVesting.sol";
import "../../src/token/VotingEscrowTruf.sol";
import "../../src/staking/VirtualStakingRewards.sol";

contract TrufVestingSherlockTest is Test {

    TruflationToken public trufToken;
    TrufVesting public vesting;
    VotingEscrowTruf public veTRUF;
    VirtualStakingRewards public trufStakingRewards;

    address public alice;
    address public owner;

    function setUp() public {
        alice = address(uint160(uint256(keccak256(abi.encodePacked("Alice")))));
        owner = address(uint160(uint256(keccak256(abi.encodePacked("Owner")))));

        vm.label(alice, "Alice");
        vm.label(owner, "Owner");

        vm.warp(1696816730);

        vm.startPrank(owner);
        trufToken = new TruflationToken();
        vesting = new TrufVesting(trufToken, uint64(block.timestamp) + 1 days);
        trufStakingRewards = new VirtualStakingRewards(owner, address(trufToken));
        veTRUF = new VotingEscrowTruf(address(trufToken), address(vesting), 1 hours, address(trufStakingRewards));
        trufStakingRewards.setOperator(address(veTRUF));
        vesting.setVeTruf(address(veTRUF));

        vm.stopPrank();
    }

    function test_sherlock_LossOfYield() external {

        vm.startPrank(owner);

            trufToken.approve(address(vesting), type(uint256).max);

            vesting.setVestingCategory(type(uint256).max, "Preseed", 171_400e18, false);

            // Initialize the original favourable vesting terms for the position:
            vesting.setVestingInfo(0, type(uint256).max, TrufVesting.VestingInfo(500, 0, 0, 24 * 30 days, 30 days));

        vm.stopPrank();

        // Enroll `alice` to a vesting position:
        vm.prank(owner);
            vesting.setUserVesting(0, 0, alice, 0, 100e18);

        vm.warp(block.timestamp + 50 days);

        // After `50 days`, `alice` can realize the following rewards:
        assertEq(vesting.claimable(0, 0, alice), 8958333333333333333);

        // The `owner` changes the terms of vesting.
        vm.prank(owner);
            vesting.setVestingInfo(0, 0, TrufVesting.VestingInfo(100, 0, 0, 24 * 30 days, 30 days));

        // `alice` is slashed their rightful earnings.
        assertEq(vesting.claimable(0, 0, alice), 5125000000000000000);

    }

}
```

## Impact

A stepwise change to reward terms like that induced by a call to [`setVestingInfo(uint256,uint256,VestingInfo)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L463C14-L464C39) is incompatible with the current vesting logic. For example, let's consider the case where a change to [`VestingInfo`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L94) which would result in a negative outcome for existing stakers:

- Rewards were `claimable` throughout the vesting cycle, meaning some users would have been able to unfairly gain from the original terms of the agreement versus other existing stakers.
- A pending call to [`setVestingInfo(uint256,uint256,VestingInfo)`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L463C14-L464C39) could be detected in the mempool, and affected stakers could front run the impending change to subvert the new terms of vesting and make off with the historical rewards that operated under more favourable terms.

## Code Snippet

```solidity
/**
 * @notice Add or modify vesting information
 * @dev Only admin can set vesting info
 * @param categoryIdx category id
 * @param id id to modify or uint256.max to add new info
 * @param info new vesting info
 */
function setVestingInfo(uint256 categoryIdx, uint256 id, VestingInfo calldata info) public onlyOwner {
    if (id == type(uint256).max) {
        id = vestingInfos[categoryIdx].length;
        vestingInfos[categoryIdx].push(info);
    } else {
        vestingInfos[categoryIdx][id] = info;
    }

    emit VestingInfoSet(categoryIdx, id, info);
}
```

## Tool used

Visual Studio Code, Foundry

## Recommendation

### [TrufVesting.sol](https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol)

When modifying [`vestingInfos`](https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/token/TrufVesting.sol#L129), ensure that the token generation event has not already occurred:

```diff
function setVestingInfo(uint256 categoryIdx, uint256 id, VestingInfo calldata info) public onlyOwner {
+   if (block.timestamp >= tgeTime) revert VestingStarted(tgeTime);
+
    if (id == type(uint256).max) {
        id = vestingInfos[categoryIdx].length;
        vestingInfos[categoryIdx].push(info);
    } else {
        vestingInfos[categoryIdx][id] = info;
    }

    emit VestingInfoSet(categoryIdx, id, info);
}
```


