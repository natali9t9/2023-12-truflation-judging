Soft Topaz Gibbon

high

# `claimable` prior to cliff time does not account for locked balance

## Summary
`claimable` prior to cliff time does not account for locked balance 

## Vulnerability Detail
After a user's vesting begins, up until the cliff time, the user can claim their `initialRelease` part of the vesting. However, in that case it is not accounted if a user has staked any balance. This would allow the user to claim more than supposed to and leave the contract underfunded. Innocent users who then try to claim their vestings will get their transactions reverted due to lack of funds. 

```solidity
    function claimable(uint256 categoryId, uint256 vestingId, address user)
        public
        view
        returns (uint256 claimableAmount)
    {
        UserVesting memory userVesting = userVestings[categoryId][vestingId][user];

        VestingInfo memory info = vestingInfos[categoryId][vestingId];

        uint64 startTime = userVesting.startTime + info.initialReleasePeriod;

        if (startTime > block.timestamp) {
            return 0;
        }

        uint256 totalAmount = userVesting.amount;

        uint256 initialRelease = (totalAmount * info.initialReleasePct) / DENOMINATOR;

        startTime += info.cliff;

        if (startTime > block.timestamp) {
            return initialRelease;
        }
```

## Impact
User will have unfairly high voting balance in `veTruf` contract
Theft of funds.
Innocent users will be unable to claim their vestings.

## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/main/truflation-contracts/src/token/TrufVesting.sol#L176C1-L183C1

## Tool used

Manual Review

## Recommendation
check `initialRelease` against `maxClaimable`

## PoC 
```solidity
    function test_cliffDoesNotConsiderLocked() public { 
        _setupVestingPlan();
        uint256 categoryId = 2;
        uint256 vestingId = 0;
        uint256 stakeAmount = 10e18;
        uint256 duration = 30 days;

        vm.startPrank(owner);
        
        vesting.setUserVesting(categoryId, vestingId, alice, 0, stakeAmount);

        vm.warp(block.timestamp + 11 days);     // warping 11 days, because initial release period is 10 days
                                                // and cliff is at 20 days. We need to be in the middle 
        vm.startPrank(alice);
        vesting.stake(categoryId, vestingId, stakeAmount, duration);    // alice stakes her full amount 

        assertEq(trufToken.balanceOf(alice), 0);
        vesting.claim(alice, categoryId, vestingId, type(uint256).max);
        
        uint256 balance = trufToken.balanceOf(alice);
        assertEq(balance, stakeAmount * 5 / 100);       // despite having staked her whole vesting balance, 
                                                        // Alice could still claim 5% 
    }
```