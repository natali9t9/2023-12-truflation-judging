Curly Boysenberry Cottonmouth

medium

# Unused rewards for a certain period will be locked forever in the `VirtualStakingRewards` contract

## Summary
Rewards are distributed to the stakers by the `VirtualStakingRewards`, but a certain period where `totalSupply` is 0, rewards are not added to the `rewardPertokenStored` and the reward for that period will not be distributed, leaving them stuck in the contract forever. 
This is an issue paramount to [forks](https://github.com/code-423n4/2022-09-y2k-finance-findings/issues/93) [of](https://github.com/code-423n4/2023-06-lybra-findings/issues/484) Synthetix Staking rewards  contract that this contract appears to be forked from.

## Vulnerability Detail
Consider the `notifyRewardAmount` function, which calculates the rewardRate is gotten by dividing the `reward` by the `rewardsDuration`. This means that a certain `rewardRate` is distributed every second betweeen stakers, till all rewards are fully distributed.
```solidity
    function notifyRewardAmount(uint256 reward) external onlyRewardsDistribution updateReward(address(0)) {
        if (block.timestamp >= periodFinish) {
            rewardRate = reward / rewardsDuration; //@note
        } else {
            uint256 remaining = periodFinish - block.timestamp;
            uint256 leftover = remaining * rewardRate;
            rewardRate = (reward + leftover) / rewardsDuration; //@note
        }

        // Ensure the provided reward amount is not more than the balance in the contract.
        // This keeps the reward rate in the right range, preventing overflows due to
        // very high values of rewardRate in the earned and rewardsPerToken functions;
        // Reward + leftover must be less than 2^256 / 10^18 to avoid overflow.
        uint256 balance = IERC20(rewardsToken).balanceOf(address(this));
        if (rewardRate > balance / rewardsDuration) {
            revert InsufficientRewards();
        }

        lastUpdateTime = block.timestamp;
        periodFinish = block.timestamp + rewardsDuration;
        emit RewardAdded(reward);
    }

```

To determine and keep track of distributed token, the `updateReward` modifier is used to update the `rewardPerTokenStored`, which uses the `rewardPerToken` function to update the `VirtualStakingRewards` 

```solidity
    modifier updateReward(address account) {
        rewardPerTokenStored = rewardPerToken(); //@note
        lastUpdateTime = lastTimeRewardApplicable();
        if (account != address(0)) {
            rewards[account] = earned(account);
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }
        _;
    }

```
and the `rewardPerToken` function, 
```solidity
    function rewardPerToken() public view returns (uint256) {
        if (_totalSupply == 0) { //@note
            return rewardPerTokenStored;
        }
        return
            rewardPerTokenStored + (((lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18) / _totalSupply);
    }

```

To better explain, 
- Admin adds rewards to make rewards rate 100 tokens per second as per the `notifyRewardAmount` function.
- For the initial 10 seconds, deposits, so no increase in `totalSupply` from 0;
- Accumulated rewards at this time is 100 * 10 = 1000 tokens which will remain in the contracts.  
- Upon subsequent rewards update, these 1000 tokens are not used, leaving them stuck forever.

During the time period where the `_totalSupply` is 0, the contract doesn't increase the `rewardPerTokenStored` and rewards accumulated during that period are stuck, can't be withdrawn.  
## Impact
Depending on how long `totalSupply` is 0, rewards will be accumulated  but not distributed.
## Code Snippet
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/staking/VirtualStakingRewards.sol#L144
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/staking/VirtualStakingRewards.sol#L53
https://github.com/sherlock-audit/2023-12-truflation/blob/37ddbb69e0c7fb6510f1ec99162fd9172ec44733/truflation-contracts/src/staking/VirtualStakingRewards.sol#L126
## Tool used
Manual Code Review

## Recommendation
Consider introducing a sweep function to withdraw these rewards, or refactoring the code to include early rewards.